
apply plugin: 'java'

group 'com.reandroid.apkeditor'
version '1.4.5'

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    // Use ARSCLib as a submodule project dependency
    compile project(':ARSCLib')

    // built from: https://github.com/REAndroid/smali-lib
    compile files("$rootProject.projectDir/libs/smali.jar")

    // built from: https://github.com/REAndroid/JCommand
    compile files("$rootProject.projectDir/libs/JCommand.jar")
}

processResources {
    filesMatching('apkeditor.properties') {
        expand('version': version)
    }
}

javadoc {
    exclude('com/reandroid/test/**')
}
jar {
    exclude('com/reandroid/test/**')
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'com.reandroid.apkeditor.Main'
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

