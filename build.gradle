
apply plugin: 'application'

group 'com.reandroid.apkeditor'
version '1.2.8'

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.compileJava {
            //options.addStringOption('-Xlint:unchecked', '-quiet')
        }
    }
}
repositories {
    mavenCentral()
}

dependencies {
    //implementation("io.github.reandroid:ARSCLib:+")
    implementation(files("$rootProject.projectDir/libs/ARSCLib.jar"))

    // built from: https://github.com/REAndroid/smali-lib
    implementation(files("$rootProject.projectDir/libs/smali.jar"))
}

processResources {
    filesMatching('apkeditor.properties') {
        expand('version': version)
    }
}

javadoc {
    exclude('com/reandroid/test/**')
}
jar {
    exclude('com/reandroid/test/**')
    duplicatesStrategy = DuplicatesStrategy.FAIL
}

application {
    mainClass = 'com.reandroid.apkeditor.Main'
}

tasks.named('run') {
    // run from root directory
    // otherwise run from brut.apktool/apktool-cli
    workingDir = file(System.getProperty('user.dir'))
}

tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'com.reandroid.apkeditor.Main'
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

